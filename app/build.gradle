apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

Properties properties = new Properties()

// This ad mob value is a test value, real value is in app/release.properties
def DEBUG_AD_MOB_APP_ID = "ca-app-pub-3940256099942544~3347511713"
def DEBUG_C_MAKE_FLAVOR = "-DFLAVOR=DEBUG"

// The real value of these attributes are in release.properties
def RELEASE_AD_MOB_APP_ID = ""
def RELEASE_C_MAKE_FLAVOR = ""

// Add your values in app/release.properties file in order to get them here
if (project.file('src/release/release.properties').exists()) {
    properties.load(project.file('src/release/release.properties').newDataInputStream())
    // If you want the app use a specific ad mob key while develop
    if (properties.getProperty('AD_MOB_APP_ID')) {
        DEBUG_AD_MOB_APP_ID = properties.getProperty('AD_MOB_APP_ID')
        RELEASE_AD_MOB_APP_ID = properties.getProperty('AD_MOB_APP_ID')
    }
    // If you want the app use an specific endpoint while develop
    if (properties.getProperty('C_MAKE_FLAVOR')) {
        DEBUG_C_MAKE_FLAVOR = properties.getProperty('C_MAKE_FLAVOR')
        RELEASE_C_MAKE_FLAVOR = properties.getProperty('C_MAKE_FLAVOR')
    }
}

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "com.yesferal.hornsapp"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 21
        versionName "2.2.6"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake { cppFlags "" }
        }
    }
    buildTypes {
        debug {
            debuggable true
            manifestPlaceholders = [crashlyticsCollectionEnabled: "false", AD_MOB_APP_ID: DEBUG_AD_MOB_APP_ID]
            externalNativeBuild.cmake {
                arguments DEBUG_C_MAKE_FLAVOR
            }
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsCollectionEnabled: "true", AD_MOB_APP_ID: RELEASE_AD_MOB_APP_ID]
            externalNativeBuild.cmake {
                arguments RELEASE_C_MAKE_FLAVOR
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = versions.jvmTarget
    }
}

dependencies {
    // Jar
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}"

    // Android
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'

    // Retrofit - API request
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit}"

    // Own Libraries
    implementation 'com.github.Yesferal:HornsApp-DI:1.2.2'
    implementation 'com.github.Yesferal:HornsApp-Android-DI:1.2.2'
    // MavenLocal Core: com.yesferal.hornsapp.core:HornsApp-Core-jvm:
    implementation 'com.github.Yesferal:HornsApp-Core:0.5.13'
    implementation 'com.github.Yesferal:DelegateAdapter:1.5.3'

    // ViewPager
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    // Picasso - Image
    implementation 'com.squareup.picasso:picasso:2.8'

    // Google
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.firebase:firebase-analytics-ktx:20.0.0'
    implementation 'com.google.android.gms:play-services-ads:20.4.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.4'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:${versions.navigation}"
    implementation "androidx.navigation:navigation-ui-ktx:${versions.navigation}"

    // Room
    implementation "androidx.room:room-runtime:${versions.room}"
    kapt "androidx.room:room-compiler:${versions.room}"

    //Socket.io
    implementation('io.socket:socket.io-client:2.0.1') {
        exclude group: 'org.json', module: 'json'
    }

    // required to avoid crash on Android 12 API 31
    implementation 'androidx.work:work-runtime-ktx:2.7.1'

    // Test Library
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
